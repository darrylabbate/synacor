op = {
    fn { exit()                        }, // halt
    fn { r[w] = y              ip += 3 }, // set
    fn { s[sp++] = x           ip += 2 }, // push
    fn { r[w] = s[--sp]        ip += 2 }, // pop
    fn { r[w] = y == z         ip += 4 }, // eq
    fn { r[w] = y >  z         ip += 4 }, // gt
    fn { ip   = x                      }, // jmp
    fn { ip   =  x ? y :       ip +  3 }, // jt
    fn { ip   = !x ? y :       ip +  3 }, // jf
    fn { r[w] = y + z & 0x7fff ip += 4 }, // add
    fn { r[w] = y * z & 0x7fff ip += 4 }, // mult
    fn { r[w] = y % z          ip += 4 }, // mod
    fn { r[w] = y & z          ip += 4 }, // and
    fn { r[w] = y | z          ip += 4 }, // or
    fn { r[w] = ~y & 0x7fff    ip += 3 }, // not
    fn { r[w] = m[y]           ip += 3 }, // rmem
    fn { m[x] = y              ip += 3 }, // wmem
    fn { s[sp++] = ip + 2      ip  = x }, // call
    fn { ip = s[--sp]                  }, // ret
    fn { putc(x)               ip += 2 }, // out
    fn { read_input()          ip += 2 }, // in
    fn {                       ip += 1 }  // noop
}

mnemonics = split("halt set push pop eq gt jmp jt jf add mult mod and or not rmem wmem call ret out in noop")

fn disas_mnemonic(m) {
    if m <= 21
        write(fmt("%-4s ", mnemonics[m]), md)
    else
        write("     ", md)
}

fn disas_addr(addr, file) {
    write(fmt("0x%04x: %04x ", addr, m[addr]), file)
}

fn disas_operands(n) {
    write(n < 1 ? "     " : fmt("%04x ", m[dp+1]), md)
    write(n < 2 ? "     " : fmt("%04x ", m[dp+2]), md)
    write(n < 3 ? "     " : fmt("%04x ", m[dp+3]), md)
}

fn disas_noop {
    disas_operands(0)
    disas_mnemonic(m[dp])
    dp += 1
}

fn disas_out {
    disas_operands(0)
    disas_mnemonic(m[dp])
    write("\"", md)
    while m[dp] == 19 {
        write(dx == '\n' ? "\\n" : char(dx), md)
        dp += 2
        dx = m[dp+1] dx = dx <= 0x7fff ? dx : r[dx&0x7fff]
    }
    write("\"", md)
}

fn disasX {
    disas_operands(1)
    disas_mnemonic(m[dp])
    write(fmt("%x", dx), md)
    dp += 2
}
fn disasXY {
    disas_operands(2)
    disas_mnemonic(m[dp])
    write(fmt("%-5x %d", dx, dy), md)
    dp += 3
}

fn disasR {
    disas_operands(1)
    disas_mnemonic(m[dp])
    write(fmt("R%x", dw), md)
    dp += 2
}

fn disasRY {
    disas_operands(2)
    disas_mnemonic(m[dp])
    write(fmt("R%-4x %x", dw, dy), md)
    dp += 3
}

fn disasRM {
    disas_operands(2)
    disas_mnemonic(m[dp])
    write(fmt("R%-4x M%x", dw, dy), md)
    dp += 3
}

fn disasRYZ {
    disas_operands(3)
    disas_mnemonic(m[dp])
    write(fmt("R%-4x %-4x %x", dw, dy, dz), md)
    dp += 4
}

fn disasMY {
    disas_operands(2)
    disas_mnemonic(m[dp])
    write(fmt("M%-4x %x", dw, dy), md)
    dp += 3
}


disas = {
    disas_noop,
    disasRY,
    disasX,
    disasR,
    disasRYZ,
    disasRYZ,
    disasX,
    disasXY,
    disasXY,
    disasRYZ,
    disasRYZ,
    disasRYZ,
    disasRYZ,
    disasRYZ,
    disasRY,
    disasRM,
    disasMY,
    disasX,
    disas_noop,
    disas_out,
    disasR,
    disas_noop,
}

fn read_input {
    if ibuf == null {
        do {
            c = getc()
            ibuf #= char(c)
        } while c != '\n'
    }
    if ibuf ~ /dump/ {
        dump_mem("memdump.s")
        ibuf = null
        "What do you do?"
        read_input()
    } elif ibuf ~ /strings/ {
        dump_strings()
        ibuf = null
        "What do you do?"
        read_input()
    } else {
        r[w] = byte(ibuf)
        ibuf = ibuf == "\n" ? null : ibuf[1..]
    }
}

fn dump_mem(filename) {
    dp = 0
    md = open(filename, "w")
    while dp < #m {
        if dp == ip
            write("=> ", md)
        else
            write("   ", md)
        disas_addr(dp, md)
        dx = m[dp+1] dx = dx <= 0x7fff ? dx : r[dx&0x7fff]
        dy = m[dp+2] dy = dy <= 0x7fff ? dy : r[dy&0x7fff]
        dz = m[dp+3] dz = dz <= 0x7fff ? dz : r[dz&0x7fff]
        dw = m[dp+1] & 0x7fff
        if m[dp] <= 21
            disas[m[dp]]()
        else
            dp += 1
        write("\n", md)
    }
    close(md)
}

fn dump_strings {
    sd = open("strings.txt", "w")
    dp = 0
    while dp < #m {
        if m[dp] >= ' ' && m[dp] <= '~' {
            write(fmt("%04x: ", dp), sd)
            while m[dp] >= ' ' && m[dp] <= '~' {
                write(char(m[dp]), sd)
                dp++
            }
            write("\n", sd)
        }
        dp++
    }
    close(sd)
}

bin = open(arg[1])
while !eof(bin) {
    local l = getc(bin)
    local h = getc(bin)
    m[i++] = l | (h << 8)
}
close(bin)

ip = 0
loop {
    x = m[ip+1] x = x <= 0x7fff ? x : r[x&0x7fff]
    y = m[ip+2] y = y <= 0x7fff ? y : r[y&0x7fff]
    z = m[ip+3] z = z <= 0x7fff ? z : r[z&0x7fff]
    w = m[ip+1] & 0x7fff
    op[m[ip]]()
}
